# CROW-IMBOR (2020-08)
# Geef me alle Eigenschappen van een Beheerobject [filURI] (inclusief overerving) 
# rdfs:subClassOf* werkt niet op het CROW LDP. Vandaar dat we deze manuele UNION moeten uitvoeren.
# Getest tegen: http://linkeddata.crow.nl/publication-v2/contexts/cpc/

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX nta8035: <https://w3id.org/def/basicsemantics-owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX groep: <http://linkeddata.crow.nl/publication-v2/ns/crow/imbor/def/groepering/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?EigenschapURI ?EigenschapLabel ?EigenschapDefinitie ?VastewaardeLijstURI ?Datatype ?Eenheid ?Grootheid ?Versie

WHERE {

# Geef de object URI.
	BIND (IRI("{{&filURI}}") as ?FysiekObjectURI)
    
# Met alleen objecten die een preflabel hebben ... 
            ?FysiekObjectURI skos:prefLabel ?FysiekObjectLabel .

 # ... en die voorkomen in deze versie ...
    groep:Versie2020-08 skos:member ?FysiekObjectURI ;
            rdfs:label ?Versie .
 # ... filter op de juiste URI (dus niet met /version/ er in)...
     FILTER (!CONTAINS(str(?EigenschapURI), "/version/" )) 

# Trek het property-path uit elkaar om de superklasse te laten zien en gebruik de "UNION" hack om de property path bug in het CROW LDP te omzeilen.
# rdfs:subClassOf* werkt niet op het CROW LDP. Vandaar dat we deze manuele UNION moeten uitvoeren.
            {
              ?FysiekObjectURI rdfs:subClassOf [
                  rdfs:subClassOf [
                    rdfs:subClassOf [
                      rdfs:subClassOf [
                        rdfs:subClassOf [
                          rdfs:subClassOf [
                            rdfs:subClassOf ?SuperKlasse
                          ]
                        ]
                      ]
                    ]
                  ]
                ] .
            } UNION {
              ?FysiekObjectURI rdfs:subClassOf [
                  rdfs:subClassOf [
                    rdfs:subClassOf [
                      rdfs:subClassOf [
                        rdfs:subClassOf [
                          rdfs:subClassOf ?SuperKlasse
                        ]
                      ]
                    ]
                  ]
                ] .
            } UNION {
              ?FysiekObjectURI rdfs:subClassOf [
                  rdfs:subClassOf [
                    rdfs:subClassOf [
                      rdfs:subClassOf [
                        rdfs:subClassOf ?SuperKlasse
                      ]
                    ]
                  ]
                ] .
            } UNION {
              ?FysiekObjectURI rdfs:subClassOf [
                  rdfs:subClassOf [
                    rdfs:subClassOf [
                      rdfs:subClassOf ?SuperKlasse
                    ]
                  ]
                ] .
            } UNION {
              ?FysiekObjectURI rdfs:subClassOf [
                  rdfs:subClassOf [
                    rdfs:subClassOf ?SuperKlasse
                  ]
                ] .
            } UNION {
              ?FysiekObjectURI rdfs:subClassOf [
                  rdfs:subClassOf ?SuperKlasse 
                ] .
            } UNION {
              ?FysiekObjectURI rdfs:subClassOf ?SuperKlasse .
            } UNION {
              ?SuperKlasse skos:prefLabel ?FysiekObjectLabel .
            }

            ?SuperKlasse skos:prefLabel ?EigenschapVanObjectLabel .
            
            ?SHACLNodeShape sh:targetClass ?SuperKlasse .
            ?SHACLNodeShape sh:property ?SHACLPropertyShape .

# Laat de relatie zien van de SHACL Shape, naar de eigenschap (inclusief via de blanknodes)...
            { ?SHACLPropertyShape sh:path/(rdf:first) ?EigenschapURI .}
    UNION   { ?SHACLPropertyShape sh:path ?EigenschapURI .}
            ?EigenschapURI skos:prefLabel ?EigenschapLabel .
	OPTIONAL {?EigenschapURI skos:definition ?EigenschapDefinitie }.
 
# ... en filter de hasPart relaties eruit om alleen echte eigenschappen te zien ...
            MINUS { ?SHACLPropertyShape sh:path nta8035:hasPart }

# ... en laat dan waar mogelijk de vastewaardelijsten zien, met hun waarden ...
OPTIONAL {   ?SHACLPropertyShape sh:class ?VastewaardeLijstURI .
 		 }

# ... en laat dan waar mogelijk de datatypes zien ...
OPTIONAL {
    	 ?SHACLPropertyShape sh:datatype ?Datatype .
         }

# ... en laat dan waar mogelijk de eenheden zien ...
OPTIONAL {   ?EigenschapURI nta8035:unit ?Eenheid .
                          
		 }
# ... en laat dan waar mogelijk de grootheden zien.
OPTIONAL {   ?EigenschapURI nta8035:quantityKind ?Grootheid .
                          
		 }	
}

ORDER BY str(?EigenschapLabel)
