# CROW-IMBOR (2020-08)
# Geef me de vastewaardelijst van een eigenschap [filURI]
# Getest tegen: http://linkeddata.crow.nl/publication-v2/contexts/cpc/

PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX nta8035: <https://w3id.org/def/basicsemantics-owl#>
PREFIX groep: <http://linkeddata.crow.nl/publication-v2/ns/crow/imbor/def/groepering/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?VastewaardeLijstURI ?VastewaardeLijstLabel ?Versie
WHERE {

# Geef de eigenschap URI.
	BIND (IRI("{{&filURI}}") as ?EigenschapURI)
   
# Selecteer alle SHACL property shapes ... 
    	?SHACLPropertyShape a sh:PropertyShape .
# ... met een relatie naar een eigenschap (inclusief via de blanknodes)...
        { ?SHACLPropertyShape sh:path/(rdf:first) ?EigenschapURI .}
UNION   { ?SHACLPropertyShape sh:path ?EigenschapURI .}

# ... en die voorkomen in deze versie ...
    groep:Versie2020-08 skos:member ?SHACLPropertyShape ;
            rdfs:label ?Versie .

# ... en laat dan waar mogelijk de vastewaardelijsten zien, met hun waarden ...
OPTIONAL {   ?SHACLPropertyShape sh:class ?VastewaardeLijstURI .
					?VastewaardeLijstURI skos:prefLabel ?VastewaardeLijstLabel .
 		 }
} 