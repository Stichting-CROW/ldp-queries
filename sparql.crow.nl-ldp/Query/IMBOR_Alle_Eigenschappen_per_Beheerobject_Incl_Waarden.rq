# CROW-IMBOR (2020-08)
# Geef me alle eigenschappen van een bepaald object [filNam]. En laat zien welke vastewaardelijst erbij hoort, en/of grootheid & eenheid en/of dat het een annotation property is
# Getest tegen: http://linkeddata.crow.nl/publication-v2/contexts/cpc/select

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX nta8035: <https://w3id.org/def/basicsemantics-owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX groep: <http://linkeddata.crow.nl/publication-v2/ns/crow/imbor/def/groepering/>

SELECT ?FysiekObjectURI ?FysiekObjectLabel ?EigenschapURI ?EigenschapLabel ?VastewaardeURI ?VastewaardeLabel ?VastewaardeLijstURI ?VastewaardeLijstLabel ?Datatype ?Eenheid ?Grootheid ?Versie

WHERE {

# Selecteer alleen Fysieke objecten en hun subklasses ... 
		    ?FysiekObjectURI rdfs:subClassOf* nta8035:PhysicalObject.
            ?FysiekObjectURI skos:prefLabel ?preflabel .
 # ... die voorkomen in deze versie ...
    groep:Versie2020-08 skos:member ?FysiekObjectURI ;
            rdfs:label ?Versie .

			#FILTER (LCASE(str(?preflabel)) = "{{&filNam}}") # heel woord
			#FILTER (regex(?preflabel, "{{&filNam}}", "i")) # gedeelte
			FILTER (CONTAINS(LCASE(?preflabel), "{{&filNam}}")) # gedeeltelijke match (sneller dan regex?)
			BIND (?preflabel AS ?FysiekObjectLabel)
	
# ... en die een SHACL shape hebben, inclusief overgeerfde.
            ?SHACLNodeShape sh:targetClass/^rdfs:subClassOf* ?FysiekObjectURI .
            ?SHACLNodeShape sh:property ?SHACLPropertyShape .

# Met een relatie naar een eigenschap (inclusief via de blanknodes)...
        { ?SHACLPropertyShape sh:path/(rdf:first) ?EigenschapURI .}
UNION   { ?SHACLPropertyShape sh:path ?EigenschapURI .}
        ?EigenschapURI skos:prefLabel ?EigenschapLabel .

# ... en filter de hasPart relaties eruit om alleen echte eigenschappen te zien...
			MINUS { ?SHACLPropertyShape sh:path nta8035:hasPart }

# ... en laat dan waar mogelijk de vastewaardelijsten zien, met hun waarden ...
OPTIONAL {   ?SHACLPropertyShape sh:class ?VastewaardeLijstURI .
					?VastewaardeLijstURI skos:prefLabel ?VastewaardeLijstLabel .
					?VastewaardeURI a ?VastewaardeLijstURI .
    				?VastewaardeURI skos:prefLabel ?VastewaardeLabel
   		 }

# ... en laat dan waar mogelijk de datatypes zien ...
OPTIONAL {
    	?SHACLPropertyShape sh:datatype ?Datatype .
        }

# ... en laat dan waar mogelijk de eenheden zien ...
OPTIONAL {   ?EigenschapURI nta8035:unit ?Eenheid .
                          
		 }

# ... en laat dan waar mogelijk de grootheden zien.
OPTIONAL {   ?EigenschapURI nta8035:quantityKind ?Grootheid .
                          
		 }
}
ORDER BY str(?FysiekObjectLabel) str(?EigenschapLabel)