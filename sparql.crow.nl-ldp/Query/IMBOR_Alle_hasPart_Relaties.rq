# CROW-IMBOR (v1.2.0)
# Geef me een lijst met 'hasPart' relaties binnen IMBOR
# Getest tegen: http://linkeddata.crow.nl/publication-v2/contexts/cpc/select

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX nta8035: <https://w3id.org/def/basicsemantics-owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX sh: <http://www.w3.org/ns/shacl#>

SELECT ?FysiekObjectURI ?FysiekObjectLabel ?FysiekObjectmetOnderdeelURI ?FysiekObjectmetOnderdeelLabel
WHERE {
# Selecteer alleen fysieke objecten ...
    ?FysiekObjectURI rdfs:subClassOf* nta8035:PhysicalObject ; 
         			  skos:prefLabel ?FysiekObjectLabel .
    FILTER regex(str(?FysiekObjectURI), "crow/imbor")     			  
# ... en die een SHACL shape hebben ...
            ?SHACLNodeShape sh:targetClass ?FysiekObjectURI .
    		?SHACLNodeShape sh:property [ 
# ... met de nta8035:hasPart property (en dus een onderdeel relatie zijn)  ...
        				sh:path nta8035:hasPart;
# ... en laat het onderdeel zien.
            			sh:class ?FysiekObjectmetOnderdeelURI ].
    		?FysiekObjectmetOnderdeelURI skos:prefLabel ?FysiekObjectmetOnderdeelLabel .
}
ORDER BY str(?FysiekObjectLabel) str(?FysiekObjectmetOnderdeelLabel)
