# Onderdeel A van de spreadsheet export van IMBOR-LD 2020-08
# Geef me alle ObjectTypen en Alle Eigenschappen (inclusief over geerfde) 
# Getest tegen: http://linkeddata.crow.nl/publication-v2/contexts/cpc/

PREFIX nta8035: <https://w3id.org/def/basicsemantics-owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX groep: <http://linkeddata.crow.nl/publication-v2/ns/crow/imbor/def/groepering/>

SELECT ?FysiekObjectURI ?FysiekObjectLabel ?EigenschapURI ?EigenschapLabel ?EigenschapVanObjectLabel ?Versie

WHERE {
            ?FysiekObjectURI skos:prefLabel ?preflabel .

 # ... en die voorkomen in deze versie ...
    groep:Versie2020-08 skos:member ?FysiekObjectURI ;
            rdfs:label ?Versie .

# ... met deze parameter.

            BIND (?preflabel AS ?FysiekObjectLabel)
            
# Trek het property-path uit elkaar om de superklasse te laten zien.
            ?FysiekObjectURI rdfs:subClassOf* ?SuperKlasse .
            ?SuperKlasse skos:prefLabel ?EigenschapVanObjectLabel .
            
            ?SHACLNodeShape sh:targetClass ?SuperKlasse .
            ?SHACLNodeShape sh:property ?SHACLPropertyShape .

# Laat de relatie zien van de SHACL Shape, naar de eigenschap (inclusief via de blanknodes)...
            { ?SHACLPropertyShape sh:path/(rdf:first) ?EigenschapURI .}
    UNION   { ?SHACLPropertyShape sh:path ?EigenschapURI .}
            ?EigenschapURI skos:prefLabel ?EigenschapLabel .

# ... en filter de hasPart relaties eruit om alleen echte eigenschappen te zien.
            MINUS { ?SHACLPropertyShape sh:path nta8035:hasPart }
    
}

ORDER BY str(?FysiekObjectLabel) str(?EigenschapVanObjectLabel) str(?EigenschapLabel)
