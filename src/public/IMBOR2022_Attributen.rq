# Alle attributen met hun directe informatie

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX coll: <https://data.crow.nl/rest-api/id#>
PREFIX restapi: <https://data.crow.nl/rest-api/def#>
PREFIX imbor: <https://data.crow.nl/imbor/def/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX nen2660: <https://w3id.org/nen2660/def#>

SELECT DISTINCT ?uriConcept ?labelConcept ?definitieConcept ?toelichtingConcept ?uriSupertype ?uriGrootheid

WHERE { 
    
# Selecteer alle attributen en optioneel hun parents.   
	?uriConcept a rdf:Property .
    OPTIONAL {?uriConcept rdfs:subPropertyOf ?uriSupertype }.
    
# Kies alleen degene die voor de REST-API gedefinieerd zijn als "IMBOR", "versie 2022" en in de "IMBOR Kern" zitten.
# (Omdat het hostingplatform van het SPARQL-endpoint geen grafen onderscheidt, is deze tussenstap (voorlopig) nodig)
    coll:IMBOR restapi:hasMember ?uriConcept .
    coll:IMBOR-versie-2022 restapi:hasMember ?uriConcept .
    imbor: restapi:hasMember ?uriConcept .

# Selecteer Label, Synoniem, Definitie, Toelichting waar aanwezig. 
    OPTIONAL{?uriConcept skos:prefLabel ?labelConcept}
    OPTIONAL{?uriConcept skos:definition ?definitieConcept}
    OPTIONAL{?uriConcept skos:note ?toelichtingConcept}

# Indien mogelijk laat het quantitykind erbij zien (grootheid)
    OPTIONAL{?uriConcept nen2660:hasQuantityKind ?uriGrootheid }

} 
ORDER BY STR(?labelConcept) STR(?labelSupertype)
